/* =========================================== *
 *         MAIN COMPONENT INSTALLATION         *
 * =========================================== */


/* -------------------------------------------------------------------------------------------------- *
 *   	ADDs CamDawg's custom IsValidForPartyDialogue state used throughout isra. Thanks Cam!         *
 * -------------------------------------------------------------------------------------------------- *
 *  covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH,          *
 *  STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS,  *
 *  STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING                                         *
 * -------------------------------------------------------------------------------------------------- */
APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


/* ------------------------------ *
 *  Fixing Imoen's dialogue file  *
 * ------------------------------ */

// Tutu: Give Imoen a banter file
// ------------------------------
ACTION_IF is_tutu BEGIN
	APPEND ~interdia.2da~ ~IMOEN       _BIMOEN~ UNLESS ~_\(BIMOEN\|bimoen\)~
	COPY_EXISTING ~interdia.2da~ ~override~
		PRETTY_PRINT_2DA
END

// BGT: Add Imoen's banter file if Fixpack isn't installed
// -------------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ AND (is_bgt) BEGIN
	COPY_EXISTING ~interdia.2da~ ~override~
		SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
	BUT_ONLY
	COPY_EXISTING ~interdia.2da~ ~override~
		PRETTY_PRINT_2DA
END

// Gives Imoen a banter file if there isn't one already
// ----------------------------------------------------
ACTION_IF (is_tutu) AND NOT (FILE_EXISTS_IN_GAME ~_bimoen.dlg~) BEGIN
	<<<<<<<< isra/dialogue/_bimoen.d
BEGIN _BIMOEN
  >>>>>>>>
	COMPILE ~isra/dialogue/_bimoen.d~
END
ACTION_IF (is_bgt) AND NOT (FILE_EXISTS_IN_GAME ~bimoen2.dlg~) BEGIN
	<<<<<<<< isra/dialogue/bimoen2.d
BEGIN BIMOEN2
  >>>>>>>>
	COMPILE ~isra/dialogue/bimoen2.d~
END


/* ------------------------------------------ *
 *  Fixing .ids, courtesy of the BG2 Fixpack  *
 * ------------------------------------------ */

// Adds ToB trigger/actions to SoA
// -------------------------------
INCLUDE ~isra/lib/tob2soa.tph~

// Adds timers entry in gtimes.ids
// -------------------------------
INCLUDE ~isra/lib/ids_entries_gtimes.tph~


/* ------------------------------------------ *
 *  Areas patching                            *
 * ------------------------------------------ *
 *  - Tutu: Assignment a script to all Areas  *
 *  - Area Type Flagging:                     *
 *        - bit 0: Outdoor                    *
 *        -bit 3: City                        *
 *        -bit 4: Forest                      *
 * ------------------------------------------ */

// Tutu Area Script Assignment Patching: All Areas Script ID'd
// -----------------------------------------------------------
ACTION_IF is_tutu BEGIN
	INCLUDE ~isra/lib/tutu_area_script_assign.tph~
END

// TotSC content
// -------------
ACTION_IF (is_totsc) THEN BEGIN // if TotSC is installed (tutu, bgt and bg:ee)
	COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                  ~%IsleofBalduranS%.are~ ~override~
                  ~%DurlagsTower%.are~ ~override~
                  ~%Farmlands%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | (BIT0 | BIT4))	// FOREST and OUTDOOR (THIS BOR "0b00010001")
	BUT_ONLY
END

// BG content
// ----------
ACTION_IF (is_tutu) OR (is_bgt) OR (is_ee) THEN BEGIN
	COPY_EXISTING ~%FishingVillage%.are~ ~override~
                  ~%Peldvale%.are~ ~override~
                  ~%LionsWay%.are~ ~override~
                  ~%CoastWay%.are~ ~override~
                  ~%Larswood%.are~ ~override~
                  ~%ShipwrecksCoast%.are~ ~override~
                  ~%HighHedge%.are~ ~override~
                  ~%MutaminsGarden%.are~ ~override~
                  ~%Lighthouse%.are~ ~override~
                  ~%RedCanyons%.are~ ~override~
                  ~%SouthBeregostRoad%.are~ ~override~
                  ~%Ulcaster%.are~ ~override~
                  ~%ArchaeologicalSite%.are~ ~override~
                  ~%FishermansLake%.are~ ~override~
                  ~%NorthNashkelRoad%.are~ ~override~
                  ~%LonelyPeaks%.are~ ~override~
                  ~%FirewineBridge%.are~ ~override~
                  ~%BearRiver%.are~ ~override~
                  ~%ValleyoftheTombs%.are~ ~override~
                  ~%DryadFalls%.are~ ~override~
                  ~%FireLeafForest%.are~ ~override~
                  ~%GibberlingMountains%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | (BIT0 | BIT4))	// FOREST and OUTDOOR (THIS BOR "0b00010001")
    BUT_ONLY

	COPY_EXISTING ~%GnollStronghold%.are~ ~override~
                  ~%NashkelMines%.are~ ~override~
                  ~%FriendlyArmInn%.are~ ~override~
                  ~%Temple%.are~ ~override~
                  ~%NashkelCarnival%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | BIT0)	// OUTDOOR ONLY (THIS BOR "0b00000001")
	BUT_ONLY

	COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
                  ~%Candlekeep_Ch6%.are~ ~override~
                  ~%Gullykin%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | (BIT0 | BIT3))	// CITY and OUTDOOR (THIS BOR "0b00001001")
	BUT_ONLY
END


/* ------------------------ *
 *  Installing audio files  *
 * ------------------------ */

LAF HANDLE_AUDIO END


/* ---------------------------------- *
 *  Installing dialogues and scripts  *
 * ---------------------------------- */

// Compiling dialogues...
COMPILE EVALUATE_BUFFER ~isra/dialogue/rh#isra.d~
COMPILE EVALUATE_BUFFER ~isra/dialogue/rh#israj.d~
COMPILE EVALUATE_BUFFER ~isra/dialogue/brh#isra.d~
COMPILE EVALUATE_BUFFER ~isra/dialogue/rh#israp.d~

// Compiling scripts...
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#isra.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#israd.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#israx.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#ismes.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#isfix.baf~	USING ~isra/translations/%s\setup.tra~

EXTEND_TOP ~%NashkelMines_BCS%~ ~isra/scripts/fw5400.baf~ EVALUATE_BUFFER

EXTEND_BOTTOM ~%DYNAHEIR_BCS%.bcs~ ~isra/scripts/dynaheir.baf~ EVALUATE_BUFFER

ACTION_IF is_ee BEGIN
	COMPILE EVALUATE_BUFFER ~isra/dialogue/bgee.d~
	EXTEND_BOTTOM ~rh#isra.bcs~ ~isra/scripts/bgee_rh#isra.baf~
END

// SoD/BGT compatibility: Isra leaves the party at transition.
ACTION_IF (is_sod) OR (is_eet) BEGIN
	EXTEND_TOP ~bd0103.bcs~ ~isra/scripts/bd0103.baf~
END ELSE BEGIN
	ACTION_IF (is_bgt) BEGIN
		EXTEND_TOP ~aram00.bcs~ ~isra/scripts/aram00.baf~
	END
END


/* ---------------------- *
 *  Installing portraits  *
 * ---------------------- */
ACTION_IF is_ee BEGIN
	COPY ~isra/portraits/rh#isral.bmp~ ~override~
	COPY ~isra/portraits/rh#isral.bmp~ ~override/rh#isram.bmp~
END ELSE BEGIN
	COPY ~isra/portraits~ ~override~
END


/* ---------------------- *
 *  Installing creatures  *
 * ---------------------- */

// Messenger (bg: MTOWN2 - tutu: _MTOWN2 - bgt: BGMTOWN2 - eet: MTOWN2_)
COPY_EXISTING ~%tutu_var%%tutu_scriptbg%mtown2%eet_var%.cre~ ~override/rh#ismes.cre~
	SAY NAME1 @1006 /* Messenger */
	SAY NAME2 @1006
	WRITE_ASCII 0x248 ~rh#ismes~ #8  // override script
	WRITE_ASCII 0x280 ~rh#ismes~ #32 // DV
	WRITE_ASCII 0x2cc ~rh#ismes~ #8  // pre-joining dialogue file
BUT_ONLY

// Isra
COPY ~isra/creatures/rh#isra.cre~ ~override/rh#isra.cre~
	SAY NAME1 @1007 /* Isra */
	SAY NAME2 @1007
	WRITE_ASCII 0x248 ~rh#isra~ #8  // override script
	WRITE_ASCII 0x280 ~rh#isra~ #32 // DV
	WRITE_ASCII 0x2cc ~rh#isra~ #8  //pre-joining dialogue file
	PATCH_IF is_ee BEGIN
		WRITE_ASCII 0x34 ~rh#isram~ #8   // small portrait
		WRITE_ASCII 0x3c ~rh#isral~ #8   // medium portrait
	END ELSE BEGIN
		WRITE_ASCII 0x34 ~rh#isras~ #8   //small portrait
		WRITE_ASCII 0x3c ~rh#isram~ #8   //medium portrait
	END
	SAY BIO @1008 /* When asked about her past, ISRA laughs and admits that there is precious little to tell thus far. She explains that she has spent the past eight years in Crimmor, the Caravan Capital of Amn, living amongst her mother's kin while squired to a paladin of Sune in the Order of the Ruby Rose. As much as she seems to enjoy speaking about the city, you are left with the impression that she is quite thrilled to finally be away from it and out in the world on her own. */
	SAY MORALE ~~
	SAY HAPPY @1010						/* I'm truly honored to be in your company. */
	SAY UNHAPPY_ANNOYED @1011			/* I don't care for some of the choices you've been making. */
	SAY UNHAPPY_SERIOUS @1012			/* I shan't simply stand by and watch this any longer. */
	SAY UNHAPPY_BREAKING_POINT @1013	/* Enough! I shall be party to this no longer. */ 
	SAY LEADER @1014					/* This is new... and not entirely unwelcome. */
	SAY TIRED @1015						/* These hours are beginning to wear on me. */
	SAY BORED @1016						/* Would that we had some Lliirans amongst us to laugh away the tedium. */
	SAY BATTLE_CRY1 @1017				/* For the Firehair! */
	SAY BATTLE_CRY2 @1018				/* For all that is good! */
	SAY BATTLE_CRY3 @1019				/* For the glory of Sune! */
	SAY BATTLE_CRY4 ~~ []
	SAY BATTLE_CRY5 ~~ []
	SAY DAMAGE ~~ [rh#is011]
	SAY DYING ~~ [rh#is012]
	SAY HURT @1022						/* A healer's aid, please! */
	SAY AREA_FOREST @1023				/* As lovely as it is here, I'm beginning to miss civilization. */
	SAY AREA_CITY @1024					/* Ah, the city, in all of its magnificence... and infamy. */
	SAY AREA_DUNGEON @1025				/* I wonder what lost marvels we might find hidden down here. */
	SAY AREA_DAY @1026					/* All things seem possible in the daylight. */
	SAY AREA_NIGHT @1027				/* 'Tis a beautiful night... at least for the moment. */
	SAY SELECT_COMMON1 @1028			/* I hear you. */
	SAY SELECT_COMMON2 @1029			/* What do you need? */
	SAY SELECT_COMMON3 @1030			/* How may I help? */
	SAY SELECT_COMMON4 @1031			/* I'm ready. */
	SAY SELECT_COMMON5 @1032			/* Yes? */
	SAY SELECT_COMMON6 @1033			/* Yes, what is it? */
	SAY SELECT_ACTION1 @1034			/* Of course. */
	SAY SELECT_ACTION2 @1035			/* Very well. */
	SAY SELECT_ACTION3 @1036			/* As you wish. */
	SAY SELECT_ACTION4 @1037			/* I shall do as you say. */
	SAY SELECT_ACTION5 @1038			/* Aye, I thought so. */
	SAY SELECT_ACTION6 @1039			/* Certainly. */
	SAY SELECT_ACTION7 @1040			/* As you would have it. */
	SAY CRITICAL_HIT ~~ [rh#is032]
	SAY CRITICAL_MISS ~~ [rh#is033]
	SAY TARGET_IMMUNE @1043				/* This does not work at all! */
	SAY INVENTORY_FULL @1044			/* That's more than I can carry. */
	SAY SELECT_RARE1 @1045				/* Hmm? */
	SAY SELECT_RARE2 @1046				/* This should prove interesting. */
	SAY SPELL_DISRUPTED @1047			/* My spell... */
	SAY SET_A_TRAP ~~ []
	SAY HIDDEN_IN_SHADOWS ~~ []
	SAY PICKED_POCKET ~~ []
	ADD_CRE_ITEM ~plat01~ #0 #0 #0 ~NONE~ ~armor~
	ADD_CRE_ITEM ~sw2h01~ #0 #0 #0 ~NONE~ ~weapon1~ EQUIP
BUT_ONLY


/* ----------------- *
 *  Appending files  *
 * ----------------- */
APPEND ~pdialog.2da~ ~RH#ISRA RH#ISRAP RH#ISRAJ RH#ISRAD~ UNLESS ~rh#Isra~

APPEND ~interdia.2da~ ~rh#Isra BRH#ISRA~ UNLESS ~rh#Isra~
