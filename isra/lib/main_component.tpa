/* =========================================== *
 *         MAIN COMPONENT INSTALLATION         *
 * =========================================== */


// Adds custom IsValidForPartyDialogue state used throughout cath
APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

// Fixing Imoen's dialogue file
  /* Tutu: Giving Imoen a banter file entry in the interdi.2da */
  ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
    APPEND ~interdia.2da~ ~IMOEN                    _BIMOEN~ UNLESS ~_\(BIMOEN\|bimoen\)~
  END
  /* BGT: Giving Imoen a banter file if the Fixpack isn't installed */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~cdbehbla.pro~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
      COPY_EXISTING ~interdia.2da~ ~override~
        SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
      BUT_ONLY_IF_IT_CHANGES
    END ELSE BEGIN
        APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
        UNLESS ~BIMOEN2~
    END
  END


    /* Imoen BIMOEN.DLG rebuild */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~_bimoen.dlg~) AND (FILE_EXISTS_IN_GAME ~FW0100.are~) THEN BEGIN
    COMPILE ~isra/dialogue/x#bimoen_tutu.d~
  END ELSE BEGIN
    ACTION_IF NOT (FILE_EXISTS_IN_GAME ~bimoen2.dlg~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
      COMPILE ~isra/dialogue/x#bimoen_bgt.d~
    END
  END

  
  /* ACTION.IDS and TRIGGER.IDS patching to ToB - thanks, Cam, if you read it */
  /* and GTIMES.IDS, ANIMATE.IDS patching to ToB, courtesy of the BG2 Fix Pack */
  INCLUDE ~isra/lib/ids_entries_cleanup.tph~
  
/* Tutu Area Script Assignment Patching: All Areas Script ID'd */
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  INCLUDE ~isra/lib/tutu_area_script_assign.tph~
END

/* Area Type Flagging */
/* ToSC only: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW1500.are~) OR (FILE_EXISTS_IN_GAME ~ARW500.are~) THEN BEGIN // if TotSC is installed
  COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                ~%IsleofBalduranS%.are~ ~override~
                ~%DurlagsTower%.are~ ~override~
                ~%Farmlands%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
  BUT_ONLY_IF_IT_CHANGES
END

  /* FOREST and OUTDOOR: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW0100.are~) OR (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
  COPY_EXISTING ~%FishingVillage%.are~ ~override~
                ~%Peldvale%.are~ ~override~
                ~%LionsWay%.are~ ~override~
                ~%CoastWay%.are~ ~override~
                ~%Larswood%.are~ ~override~
                ~%ShipwrecksCoast%.are~ ~override~
                ~%HighHedge%.are~ ~override~
                ~%MutaminsGarden%.are~ ~override~
                ~%Lighthouse%.are~ ~override~
                ~%RedCanyons%.are~ ~override~
                ~%SouthBeregostRoad%.are~ ~override~
                ~%Ulcaster%.are~ ~override~
                ~%ArchaeologicalSite%.are~ ~override~
                ~%FishermansLake%.are~ ~override~
                ~%NorthNashkelRoad%.are~ ~override~
                ~%LonelyPeaks%.are~ ~override~
                ~%FirewineBridge%.are~ ~override~
                ~%BearRiver%.are~ ~override~
                ~%ValleyoftheTombs%.are~ ~override~
                ~%DryadFalls%.are~ ~override~
                ~%FireLeafForest%.are~ ~override~
                ~%GibberlingMountains%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
    BUT_ONLY_IF_IT_CHANGES

/* OUTDOOR ONLY: Tutu and BGT */
COPY_EXISTING ~%GnollStronghold%.are~ ~override~
              ~%NashkelMines%.are~ ~override~
              ~%FriendlyArmInn%.are~ ~override~
              ~%Temple%.are~ ~override~
              ~%NashkelCarnival%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00000001")
  BUT_ONLY_IF_IT_CHANGES

/* CITY and OUTDOOR */
COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
              ~%Candlekeep_Ch6%.are~ ~override~
              ~%Gullykin%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00001001")
  BUT_ONLY_IF_IT_CHANGES
END

// Audio

COPY ~isra/audio~ ~override~

ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~) THEN BEGIN
  COPY   ~isra/israinstall.bat~   ~israinstall.bat~
  COPY + ~isra/israuninstall.bat~ ~israuninstall.bat~
    AT_INTERACTIVE_EXIT ~israinstall.bat~
  AT_UNINSTALL        ~isra/israuninstall.bat~
END ELSE BEGIN
  ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~) THEN BEGIN
  AT_INTERACTIVE_EXIT ~sh Isra/isra_install_audio.sh~
  AT_UNINSTALL        ~sh Isra/isra_uninstall_audio.sh~
  END
END

// Compiling dialogue...
COMPILE EVALUATE_BUFFER ~isra/dialogue/rh#isra.d~
COMPILE EVALUATE_BUFFER ~isra/dialogue/rh#israj.d~
COMPILE EVALUATE_BUFFER ~isra/dialogue/brh#isra.d~
COMPILE EVALUATE_BUFFER ~isra/dialogue/rh#israp.d~

// Compiling scripts...
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#isra.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#israd.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#israx.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#ismes.baf~
COMPILE EVALUATE_BUFFER ~isra/scripts/rh#isfix.baf~

EXTEND_BOTTOM ~%DYNAHEIR_BCS%.bcs~ ~isra/scripts/dynaheir.baf~
    EVALUATE_BUFFER
    
ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~isra/dialogue/bgee.d~
  EXTEND_BOTTOM ~rh#isra.bcs~ ~isra/scripts/bgee_rh#isra.baf~
END

// sod compatibility
ACTION_IF FILE_EXISTS_IN_GAME ~bdcaelar.dlg~ THEN BEGIN
  EXTEND_TOP ~bd0103.bcs~ ~Isra/scripts/bd0103.baf~
END

ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
// Area scripts
  EXTEND_TOP ~_ar5400.bcs~ ~isra/scripts/fw5400.baf~
    EVALUATE_BUFFER

// Creatures
  COPY_EXISTING ~_mtown2.cre~ ~override/rh#ismes.cre~
  SAY NAME1 @6 /* Messenger */
  SAY NAME2 @6 /* Messenger */
    WRITE_ASCII 0x248 ~rh#ismes~ #8  //override script
    WRITE_ASCII 0x280 ~rh#ismes~ #32  //DV
    WRITE_ASCII 0x2cc ~rh#ismes~ #8   //pre-joining dialogue file

END ELSE BEGIN 
  ACTION_IF FILE_EXISTS_IN_GAME ~AR7200.are~ THEN BEGIN

// Area scripts
    EXTEND_TOP ~ar4400.bcs~ ~isra/scripts/fw5400.baf~
      EVALUATE_BUFFER
    EXTEND_TOP ~aram00.bcs~ ~isra/scripts/aram00.baf~

// Creatures
  COPY_EXISTING ~mtown2.cre~ ~override/rh#ismes.cre~
  SAY NAME1 @6 /* Messenger */
  SAY NAME2 @6 /* Messenger */
    WRITE_ASCII 0x248 ~rh#ismes~ #8  //override script
    WRITE_ASCII 0x280 ~rh#ismes~ #32  //DV
    WRITE_ASCII 0x2cc ~rh#ismes~ #8   //pre-joining dialogue file
  END ELSE BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN

  // Area scripts
      EXTEND_TOP ~ar5400.bcs~ ~isra/scripts/fw5400.baf~
        EVALUATE_BUFFER
  
  // Creatures
    COPY_EXISTING ~mtown2.cre~ ~override/rh#ismes.cre~
  SAY NAME1 @6 /* Messenger */
  SAY NAME2 @6 /* Messenger */
    WRITE_ASCII 0x248 ~rh#ismes~ #8  //override script
    WRITE_ASCII 0x280 ~rh#ismes~ #32  //DV
    WRITE_ASCII 0x2cc ~rh#ismes~ #8   //pre-joining dialogue file
    END
  END
END



// Copying images and creatures

COPY ~isra/creatures/rh#isra.cre~ ~override/rh#isra.cre~
  SAY NAME1 @7 /* Isra */
  SAY NAME2 @7 /* Isra */
  WRITE_ASCII 0x248 ~rh#isra~ #8  //override script
  WRITE_ASCII 0x280 ~rh#isra~ #32  //DV
  WRITE_ASCII 0x2cc ~rh#isra~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~rh#isras~ #8   //small portrait
  WRITE_ASCII 0x3c ~rh#isram~ #8   //medium portrait
  SAY BIO @8 /* When asked about her past, ISRA laughs and admits that there is precious little to tell thus far. She explains that she has spent the past eight years in Crimmor, the Caravan Capital of Amn, living amongst her mother's kin while squired to a paladin of Sune in the Order of the Ruby Rose. As much as she seems to enjoy speaking about the city, you are left with the impression that she is quite thrilled to finally be away from it and out in the world on her own. */
  SAY MORALE ~~
  SAY HAPPY @10 /* I'm truly honored to be in your company. */
  SAY UNHAPPY_ANNOYED @11 /* I don't care for some of the choices you've been making. */
  SAY UNHAPPY_SERIOUS @12 /* I shan't simply stand by and watch this any longer. */
  SAY UNHAPPY_BREAKING_POINT @13 /* Enough! I shall be party to this no longer. */ 
  SAY LEADER @14 /* This is new... and not entirely unwelcome. */
  SAY TIRED @15 /* These hours are beginning to wear on me. */
  SAY BORED @16 /* Would that we had some Lliirans amongst us to laugh away the tedium. */
  SAY BATTLE_CRY1 @17 /* For the Firehair! */
  SAY BATTLE_CRY2 @18 /* For all that is good! */
  SAY BATTLE_CRY3 @19 /* For the glory of Sune! */
  SAY BATTLE_CRY4 ~~ []
  SAY BATTLE_CRY5 ~~ []
  SAY DAMAGE ~~ [rh#is011]
  SAY DYING ~~ [rh#is012]
  SAY HURT @22 /* A healer's aid, please! */
  SAY AREA_FOREST @23 /* As lovely as it is here, I'm beginning to miss civilization. */
  SAY AREA_CITY @24 /* Ah, the city, in all of its magnificence... and infamy. */
  SAY AREA_DUNGEON @25 /* I wonder what lost marvels we might find hidden down here. */
  SAY AREA_DAY @26 /* All things seem possible in the daylight. */
  SAY AREA_NIGHT @27 /* 'Tis a beautiful night... at least for the moment. */
  SAY SELECT_COMMON1 @28 /* I hear you. */
  SAY SELECT_COMMON2 @29 /* What do you need? */
  SAY SELECT_COMMON3 @30 /* How may I help? */
  SAY SELECT_COMMON4 @31 /* I'm ready. */
  SAY SELECT_COMMON5 @32 /* Yes? */
  SAY SELECT_COMMON6 @33 /* Yes, what is it? */
  SAY SELECT_ACTION1 @34 /* Of course. */
  SAY SELECT_ACTION2 @35 /* Very well. */
  SAY SELECT_ACTION3 @36 /* As you wish. */
  SAY SELECT_ACTION4 @37 /* I shall do as you say. */
  SAY SELECT_ACTION5 @38 /* Aye, I thought so. */
  SAY SELECT_ACTION6 @39 /* Certainly. */
  SAY SELECT_ACTION7 @40 /* As you would have it. */
  SAY CRITICAL_HIT ~~ [rh#is032]
  SAY CRITICAL_MISS ~~ [rh#is033]
  SAY TARGET_IMMUNE @43 /* This does not work at all! */
  SAY INVENTORY_FULL @44 /* That's more than I can carry. */
  SAY SELECT_RARE1 @45 /* Hmm? */
  SAY SELECT_RARE2 @46 /* This should prove interesting. */
  SAY SPELL_DISRUPTED @47 /* My spell... */
  SAY SET_A_TRAP ~~ []
  SAY HIDDEN_IN_SHADOWS ~~ []
  SAY PICKED_POCKET ~~ []
  ADD_CRE_ITEM ~plat01~ #0 #0 #0 ~NONE~ ~armor~
  ADD_CRE_ITEM ~sw2h01~ #0 #0 #0 ~NONE~ ~weapon1~ EQUIP

ACTION_IF (GAME_IS ~bgee eet~) BEGIN
  COPY ~isra/images/rh#isral.bmp~ ~override~
  COPY ~override/rh#isra.cre~ ~override/rh#isra.cre~
  WRITE_ASCII 0x34 ~rh#israL~ #8   // small portrait
  WRITE_ASCII 0x3c ~rh#israL~ #8   // large portrait
END ELSE BEGIN
COPY ~isra/images~ ~override~
END

// Appends
APPEND ~pdialog.2da~ ~RH#ISRA RH#ISRAP RH#ISRAJ RH#ISRAD~
  UNLESS ~rh#Isra~

APPEND ~interdia.2da~ ~rh#Isra BRH#ISRA~
  UNLESS ~rh#Isra~
