/* ------------------- *
 *  INITIATE DIALOGUE  *
 * ------------------- */
IF
	Global("rh#IsraInitiateBG1","GLOBAL",0)
	See(Player1)
	CombatCounter(0)
	Allegiance(Myself,NEUTRAL)
	Dead("Greywolf")
	Dead("Prism")
	!Dead("rh#Isra")
THEN
	RESPONSE #100
		SetGlobal("rh#IsraInitiateBG1","GLOBAL",1)
END

IF
	Global("rh#IsraInitiateBG1","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* ---------------- *
 *  GREYWOLF FIGHT  *
 * ---------------- */
IF
	!Dead("Greywolf")
	Global("rh#IsraGreywolfAttacks3","GLOBAL",0)
	OR(2) Global("rh#IsraGreywolfAttacks","GLOBAL",1) Global("rh#IsraGreywolfAttacks2","GLOBAL",1) 
THEN
	RESPONSE #100
		SetGlobal("rh#IsraGreywolfAttacks3","GLOBAL",1)
		Attack("Greywolf")
END


/* ------------ *
 *  FRIENDSHIP  *
 * ------------ */
IF
    InParty(Myself)
    Dead("mulahey")
    ReputationGT(Player1,9)
    AreaType([OUTDOOR])
    Global("rh#IsraTalksBG1","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#IsraTalksBG1Timer","GLOBAL",300)
		SetGlobal("rh#IsraTalksBG1","GLOBAL",1)
END

IF
	InParty(Myself)
	Alignment(Player1,MASK_GOOD)
	CheckStatGT(Player1,12,CHR)
	Global("rh#IsraRomanceCheck","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraRomanceCheck","GLOBAL",1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,9)
	Global("rh#IsraRomanceCheck","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraRomanceCheck","GLOBAL",2)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1Timer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	!Global("rh#IsraTalksBG1Interrupted","GLOBAL",1)
    OR(9)
		Global("rh#IsraTalksBG1","GLOBAL",2)
		Global("rh#IsraTalksBG1","GLOBAL",4)
		Global("rh#IsraTalksBG1","GLOBAL",6)
		Global("rh#IsraTalksBG1","GLOBAL",8)
		Global("rh#IsraTalksBG1","GLOBAL",10)
		Global("rh#IsraTalksBG1","GLOBAL",12)
		Global("rh#IsraTalksBG1","GLOBAL",14)
		Global("rh#IsraTalksBG1","GLOBAL",18)
		Global("rh#IsraTalksBG1","GLOBAL",20)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1Timer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    !Global("rh#IsraTalksBG1Interrupted","GLOBAL",1)
    OR(6)
		Global("rh#IsraTalksBG1","GLOBAL",1)
		Global("rh#IsraTalksBG1","GLOBAL",5)
		Global("rh#IsraTalksBG1","GLOBAL",7)
		Global("rh#IsraTalksBG1","GLOBAL",11)
		Global("rh#IsraTalksBG1","GLOBAL",13)
		Global("rh#IsraTalksBG1","GLOBAL",19)
THEN
	RESPONSE #100
		IncrementGlobal("rh#IsraTalksBG1","GLOBAL",1)
END

// 2. Messenger
// ------------
IF
    InParty(Myself)
    See(Player1)
    AreaType([CITY])
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksMessengerSpawnTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	Global("rh#IsraTalksBG1","GLOBAL",3)
	Global("rh#IsraMessengerSpawn","GLOBAL",0)
THEN
	RESPONSE #100
		CreateCreatureObjectOffset("rh#ismes",Myself,[-15.-15])
		RealSetGlobalTimer("rh#IsraMessengerRespawnTimer","GLOBAL",300)
		SetGlobal("rh#IsraMessengerSpawn","GLOBAL",1)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("rh#IsraMessengerRespawnTimer","GLOBAL")
	Global("rh#IsraMessengerSpawn","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraMessengerSpawn","GLOBAL",0)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1Timer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraMessengerSpawn","GLOBAL",2)
    Global("rh#IsraTalksBG1","GLOBAL",3)   
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#IsraTalksBG1Timer","GLOBAL",5) 
		IncrementGlobal("rh#IsraTalksBG1","GLOBAL",1)
END

// 5. Morning in an Inn
// --------------------
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1Timer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    !Global("rh#IsraTalksBG1Interrupted","GLOBAL",1)
    PartyRested()
    OR(21)
     	AreaCheck("%Beregost_FeldepostsInn_L1%")
     	AreaCheck("%Beregost_JovialJuggler_L1%")
     	AreaCheck("%Beregost_RedSheaf_L1%")
     	AreaCheck("%Beregost_BurningWizard_L1%")
     	AreaCheck("%Nashkel_Inn%")
     	AreaCheck("%EBaldursGate_KeexieTavern_L1%")
     	AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
     	AreaCheck("%EBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
     	AreaCheck("%BaldursGateDocks_JopalinsTavern%")
     	AreaCheck("%SWBaldursGate_Tavern_L1%")
     	AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
     	AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
     	AreaCheck("%NWBaldursGate_Tavern_L1%")
     	AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
     	AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
     	AreaCheck("%FriendlyArmInn_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
     	AreaCheck("%UlgothsBeard_Inn%")
    Global("rh#IsraTalksBG1","GLOBAL",9)
THEN
	RESPONSE #100
	IncrementGlobal("rh#IsraTalksBG1","GLOBAL",1)
END

// 8-9. Post Davaeorn
// ------------------
IF
    InParty(Myself)
    Dead("davaeorn")
    AreaType([OUTDOOR])
    Global("FREESLAV","GLOBAL",1)
    ReputationGT(Player1,9)
    Global("rh#IsraTalksBG1Chapter5","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#IsraTalksBG1TimerChapter5","GLOBAL",300)
		SetGlobal("rh#IsraTalksBG1Interrupted","GLOBAL",1)
		SetGlobal("rh#IsraTalksBG1Chapter5","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1TimerChapter5","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    OR(2)
		Global("rh#IsraTalksBG1Chapter5","GLOBAL",1)
		Global("rh#IsraTalksBG1Chapter5","GLOBAL",3)
THEN
	RESPONSE #100
		IncrementGlobal("rh#IsraTalksBG1Chapter5","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1TimerChapter5","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    OR(2)
      Global("rh#IsraTalksBG1Chapter5","GLOBAL",2)
      Global("rh#IsraTalksBG1Chapter5","GLOBAL",4)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

// 11. Candlekeep
// --------------
IF
    InParty(Myself)
    PartyHasItem("%tutu_var%SCRL2J")
    ReputationGT(Player1,9)
    Global("rh#IsraTalksBG1Candlekeep","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#IsraTalksBG1TimerCandlekeep","GLOBAL",15)
		SetGlobal("rh#IsraTalksBG1Candlekeep","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1TimerCandlekeep","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraTalksBG1Candlekeep","GLOBAL",1)
THEN
	RESPONSE #100
		IncrementGlobal("rh#IsraTalksBG1Candlekeep","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1TimerCandlekeep","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraTalksBG1Candlekeep","GLOBAL",2)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

// 12. Post Candlekeep
// -------------------
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("rh#IsraTalksBG1Timer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    !Global("rh#IsraTalksBG1Interrupted","GLOBAL",1)
    Global("rh#IsraTalksBG1","GLOBAL",17)
    Global("Chapter","GLOBAL",%tutu_chapter_7%)
    OR(9)
    	AreaCheck("%NWBaldursGate%")
    	AreaCheck("%NBaldursGate%")
    	AreaCheck("%NEBaldursGate%")
    	AreaCheck("%EBaldursGate%")
    	AreaCheck("%WBaldursGate%")
    	AreaCheck("%SEBaldursGate%")
    	AreaCheck("%SWBaldursGate%")
    	AreaCheck("%BaldursGateDocks%")
    	AreaCheck("%CentralBaldursGate%")  
THEN
	RESPONSE #100
	IncrementGlobal("rh#IsraTalksBG1","GLOBAL",1)
END

// Safana Banter
// -------------
IF
    InParty(Myself)
    See("Safana")
	!StateCheck("Safana",CD_STATE_NOTVALID)
	!StateCheck("rh#Isra",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("rh#IsraSafana2","GLOBAL",0)
    PartyRested()
    OR(21)
     	AreaCheck("%Beregost_FeldepostsInn_L1%")
     	AreaCheck("%Beregost_JovialJuggler_L1%")
     	AreaCheck("%Beregost_RedSheaf_L1%")
     	AreaCheck("%Beregost_BurningWizard_L1%")
     	AreaCheck("%Nashkel_Inn%")
     	AreaCheck("%EBaldursGate_KeexieTavern_L1%")
     	AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
     	AreaCheck("%EBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
     	AreaCheck("%BaldursGateDocks_JopalinsTavern%")
     	AreaCheck("%SWBaldursGate_Tavern_L1%")
     	AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
     	AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
     	AreaCheck("%NWBaldursGate_Tavern_L1%")
     	AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
     	AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
     	AreaCheck("%FriendlyArmInn_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
     	AreaCheck("%UlgothsBeard_Inn%")
THEN
	RESPONSE #100
		IncrementGlobal("rh#IsraSafana2","GLOBAL",1)
END

IF
    InParty(Myself)
    See("Safana")
	!StateCheck("Safana",CD_STATE_NOTVALID)
	!StateCheck("rh#Isra",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("rh#IsraSafana2","GLOBAL",1)
THEN
	RESPONSE #100
		Dialogue("Safana")
END

// Viconia Banter
// --------------
IF
    InParty(Myself)
    InParty("Viconia")
    See("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	!StateCheck("rh#Isra",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	HPPercentLT("rh#Isra",50)
	Global("rh#IsraViconia2_BG1","GLOBAL",0)
THEN
	RESPONSE #100
		IncrementGlobal("rh#IsraViconia2_BG1","GLOBAL",1)
END

IF
    InParty(Myself)
    See("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	!StateCheck("rh#Isra",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("rh#IsraViconia2_BG1","GLOBAL",1)
THEN
	RESPONSE #100
		Dialogue("Viconia")
END

// Tranzig EXP
// -----------
IF
	InParty(Myself)
	!Dead("Tranzig")
	CombatCounter(0)
    !See([ENEMY])
    Global("rh#IsraTranzigSpared","GLOBAL",1)
THEN
	RESPONSE #100
		AddexperienceParty(1050)
		SetGlobal("rh#IsraTranzigSpared","GLOBAL",2)
END


/* --------------- *
 *  INTERJECTIONS  *
 * --------------- */

// Cloakwood mines attempted flooding
// ----------------------------------
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    GlobalGT("FLOODED","GLOBAL",0)
    Global("rh#IsraCloakwoodDrowned","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraCloakwoodDrowned","GLOBAL",2)
END

// Dynaheir attacked
// -----------------
IF
	AreaCheck("%GnollStronghold%")
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!InParty("dynaheir")
	See("dynaheir")
	!Allegiance("dynaheir",ENEMY)
	Global("rh#IsraDefendDynaheir","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraDefendDynaheir","GLOBAL",1)
END

IF
	AreaCheck("%GnollStronghold%")
	Global("rh#IsraDefendDynaheir","GLOBAL",1)
	Global("rh#PartyAttackedDynaheir","GLOBAL",1)
	Global("rh#IsraDynaheirHostile","GLOBAL",0)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InMyArea(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraDynaheirHostile","GLOBAL",1)
END

IF
	Global("rh#IsraDynaheirHostile","GLOBAL",1)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InMyArea(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

// Girdle of Masculinity
// ---------------------
IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	HasItemEquiped("%tutu_var%BELT05",Myself) 
	Global("rh#IsraGirdle","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraGirdle","GLOBAL",1)
END

// Brage
// -----
IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	See("Brage")
	!Dead("Brage")
	Global("OublekBounty1","GLOBAL",1)
	Global("rh#IsraBrageFound","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraBrageFound","GLOBAL",1)
END

// Gallor
// ------
IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	Global("HelpGallor","GLOBAL",1)
	Global("rh#IsraGallorHelped","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraGallorHelped","GLOBAL",1)
END

// Undercellar
// -----------
IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%BaldursGate_Undercellars%")
	Global("rh#IsraUndercellar","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#IsraTalksUndercellarTimer","GLOBAL",15)
		SetGlobal("rh#IsraUndercellar","GLOBAL",1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%BaldursGate_Undercellars%")
	RealGlobalTimerExpired("rh#IsraTalksUndercellarTimer","GLOBAL")
	Global("rh#IsraUndercellar","GLOBAL",1)
	CombatCounter(0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("rh#IsraUndercellar","GLOBAL",2)
END

// Kristin & Slythe
// ----------------
IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%BaldursGate_Undercellars%")
	Global("rh#IsraKrystinSlythe","GLOBAL",0)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
	Dead("krystin")
	Dead("slythe")
THEN
	RESPONSE #100
		SetGlobal("rh#IsraKrystinSlythe","GLOBAL",1)
END

// Ramazith & Abela
// ----------------
IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("%NBaldursGate%")
	Global("rh#IsraRamazith","GLOBAL",1)
	!Dead("ramazith")
THEN
	RESPONSE #100
		SetGlobal("rh#IsraRamazith","GLOBAL",2)
END


/* --------- *
 *  low rep  *
 * --------- */
IF
	InParty(Myself)
    !ReputationGT(Player1,9)
	Global("rh#IsraLowRep","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraLowRep","GLOBAL",1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,5)
	Global("rh#IsraRepBreak","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#IsraRepBreak","GLOBAL",1)
END


/* ------------------------------- *
 *  initiating interjection talks  *
 * ------------------------------- */
IF
	InParty(Myself)
	InMyArea(Player1)
	See(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	OR(9)
    	Global("rh#IsraCloakwoodDrowned","GLOBAL",2)
		Global("rh#IsraGirdle","GLOBAL",1)
		Global("rh#IsraBrageFound","GLOBAL",1)
		Global("rh#IsraGallorHelped","GLOBAL",1)
		Global("rh#IsraKrystinSlythe","GLOBAL",1)
		Global("rh#IsraLowRep","GLOBAL",1)
		Global("rh#IsraRepBreak","GLOBAL",1)
		Global("rh#IsraRamazith","GLOBAL",2)
		Global("rh#IsraUndercellar","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* ------------ *
 *  IF HOSTILE  *
 * ------------ */
IF
	Allegiance(Myself,ENEMY)
	!InParty(Myself)
	Global("rh#NPCFight","LOCALS",0)
THEN
	RESPONSE #100
		ChangeAIScript("rh#israx",CLASS)
		ChangeAIScript("useitem",GENERAL)
		ChangeAIScript("",DEFAULT)
		SetGlobal("rh#NPCFight","LOCALS",1)
END